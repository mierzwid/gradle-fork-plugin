plugins {
    id 'java-gradle-plugin'
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
}

group 'com.neva.gradle'
version '1.0.7'
description = 'Gradle Fork Plugin'
defaultTasks = ['clean', 'publishToMavenLocal']

repositories {
    mavenLocal()
    jcenter()
    maven { url  "https://dl.bintray.com/neva-dev/maven-public" }
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.51"
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'commons-io:commons-io:2.4'
    implementation 'commons-validator:commons-validator:1.6'
    implementation 'com.neva.commons:gitignore-file-filter:1.0.0'
    implementation 'com.miglayout:miglayout:3.7.4'
    implementation 'com.mitchellbosecke:pebble:2.4.0'

    testImplementation 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

gradlePlugin {
    plugins {
        fork {
            id = "com.neva.fork"
            implementationClass = "com.neva.gradle.fork.ForkPlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.properties.get('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.properties.get('bintrayKey') ?: System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven-public'
        name = 'gradle-fork-plugin'
        userOrg = 'neva-dev'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/neva-dev/gradle-fork-plugin.git'
        labels = ['gradle', 'archetype', 'template']
        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            vcsTag = project.version
        }
    }
    publish = true
}
