plugins {
    id 'java-gradle-plugin'
    id "org.jetbrains.kotlin.jvm" version "1.2.0"
    id "com.jfrog.bintray" version "1.7.3"
    id 'maven-publish'
}

group 'com.neva.gradle'
version '1.0.1'
description = 'Gradle Fork Plugin'
defaultTasks = ['clean', 'publishToMavenLocal']

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.0"
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile 'commons-validator:commons-validator:1.6'
    compile 'com.neva.commons:gitignore-file-filter:1.0.0'
    compile group: 'com.miglayout', name: 'miglayout', version: '3.7.4'
    compile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

gradlePlugin {
    plugins {
        fork {
            id = "com.neva.fork"
            implementationClass = "com.neva.gradle.fork.ForkPlugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.properties.get('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.properties.get('bintrayKey') ?: System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven-public'
        name = 'gradle-fork-plugin'
        userOrg = 'neva-dev'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/neva-dev/gradle-fork-plugin.git'
        labels = ['gradle', 'archetype', 'template']
        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            vcsTag = project.version
        }
    }
    publish = true
}
